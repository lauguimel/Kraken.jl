using Test
using Kraken

function _stream_cell_id(topology, level, i, j)
    for (id, cell) in pairs(topology.cells)
        if cell.level == level && cell.i == i && cell.j == j
            return id
        end
    end
    error("cell not found: level=$level i=$i j=$j")
end

function _stream_zero_boundary_packets!(coarse_F, patch, topology)
    for idx in topology.boundary_links
        link = topology.links[idx]
        cell = topology.cells[link.src]
        if cell.level == 0
            coarse_F[cell.i, cell.j, link.q] = 0
        else
            i0 = 2 * first(patch.parent_i_range) - 1
            j0 = 2 * first(patch.parent_j_range) - 1
            patch.fine_F[cell.i - i0 + 1, cell.j - j0 + 1, link.q] = 0
        end
    end
    return coarse_F, patch
end

function _stream_zero_y_boundary_packets!(coarse_F, patch, topology)
    ny = size(coarse_F, 2)
    for idx in topology.boundary_links
        link = topology.links[idx]
        cell = topology.cells[link.src]
        cy = d2q9_cy(link.q)
        if cell.level == 0
            j_raw = cell.j + cy
            1 <= j_raw <= ny && continue
            coarse_F[cell.i, cell.j, link.q] = 0
        else
            j_raw = cell.j + cy
            1 <= j_raw <= 2 * ny && continue
            i0 = 2 * first(patch.parent_i_range) - 1
            j0 = 2 * first(patch.parent_j_range) - 1
            patch.fine_F[cell.i - i0 + 1, cell.j - j0 + 1, link.q] = 0
        end
    end
    return coarse_F, patch
end

_stream_moving_wall_delta(volume, rho_wall, wall_u, q) =
    volume * rho_wall * wall_u * d2q9_cx(q) / 6

function _stream_composite_fluid_mass(coarse_F, patch, is_solid)
    leaf = zeros(Float64, size(is_solid, 1), size(is_solid, 2), 9)
    composite_to_leaf_F_2d!(leaf, coarse_F, patch)
    total = 0.0
    for q in 1:9, j in axes(leaf, 2), i in axes(leaf, 1)
        is_solid[i, j] && continue
        total += leaf[i, j, q]
    end
    return total
end

@testset "Conservative tree route streaming 2D" begin
    Nx, Ny = 9, 10
    patch_in = create_conservative_tree_patch_2d(3:5, 4:6)
    patch_out = create_conservative_tree_patch_2d(3:5, 4:6)
    topology = create_conservative_tree_topology_2d(Nx, Ny, patch_in)

    @testset "direct same-level route moves one packet" begin
        coarse_in = zeros(Float64, Nx, Ny, 9)
        coarse_out = similar(coarse_in)
        coarse_in[1, 5, 2] = 3.25

        stream_composite_routes_interior_F_2d!(
            coarse_out, patch_out, coarse_in, patch_in, topology)

        @test coarse_out[2, 5, 2] == 3.25
        @test isapprox(active_mass_F(coarse_out, patch_out), 3.25; atol=1e-14, rtol=0)
    end

    @testset "coarse to fine face route keeps leaf-equivalent residual" begin
        coarse_in = zeros(Float64, Nx, Ny, 9)
        coarse_out = similar(coarse_in)
        patch_in = create_conservative_tree_patch_2d(3:5, 4:6)
        patch_out = create_conservative_tree_patch_2d(3:5, 4:6)
        coarse_in[2, 5, 2] = 4.0

        stream_composite_routes_interior_F_2d!(
            coarse_out, patch_out, coarse_in, patch_in, topology)

        @test patch_out.fine_F[1, 3, 2] == 1.0
        @test patch_out.fine_F[1, 4, 2] == 1.0
        @test coarse_out[2, 5, 2] == 2.0
        @test isapprox(active_mass_F(coarse_out, patch_out), 4.0; atol=1e-14, rtol=0)
    end

    @testset "coarse to fine corner route keeps leaf-equivalent residual" begin
        coarse_in = zeros(Float64, Nx, Ny, 9)
        coarse_out = similar(coarse_in)
        patch_in = create_conservative_tree_patch_2d(3:5, 4:6)
        patch_out = create_conservative_tree_patch_2d(3:5, 4:6)
        coarse_in[2, 3, 6] = 5.5

        stream_composite_routes_interior_F_2d!(
            coarse_out, patch_out, coarse_in, patch_in, topology)

        @test patch_out.fine_F[1, 1, 6] == 1.375
        @test coarse_out[2, 3, 6] == 1.375
        @test coarse_out[3, 3, 6] == 1.375
        @test coarse_out[2, 4, 6] == 1.375
        @test isapprox(active_mass_F(coarse_out, patch_out), 5.5; atol=1e-14, rtol=0)
    end

    @testset "fine to coarse face routes coalesce by accumulation" begin
        coarse_in = zeros(Float64, Nx, Ny, 9)
        coarse_out = similar(coarse_in)
        patch_in = create_conservative_tree_patch_2d(3:5, 4:6)
        patch_out = create_conservative_tree_patch_2d(3:5, 4:6)
        patch_in.fine_F[1, 3, 4] = 1.25
        patch_in.fine_F[1, 4, 4] = 2.75

        stream_composite_routes_interior_F_2d!(
            coarse_out, patch_out, coarse_in, patch_in, topology)

        @test coarse_out[2, 5, 4] == 4.0
        @test isapprox(active_mass_F(coarse_out, patch_out), 4.0; atol=1e-14, rtol=0)
    end

    @testset "fine to coarse corner route sends one packet" begin
        coarse_in = zeros(Float64, Nx, Ny, 9)
        coarse_out = similar(coarse_in)
        patch_in = create_conservative_tree_patch_2d(3:5, 4:6)
        patch_out = create_conservative_tree_patch_2d(3:5, 4:6)
        patch_in.fine_F[1, 1, 8] = 7.0

        stream_composite_routes_interior_F_2d!(
            coarse_out, patch_out, coarse_in, patch_in, topology)

        @test coarse_out[2, 3, 8] == 7.0
        @test isapprox(active_mass_F(coarse_out, patch_out), 7.0; atol=1e-14, rtol=0)
    end

    @testset "all non-boundary routes conserve active population sums" begin
        coarse_in = zeros(Float64, Nx, Ny, 9)
        coarse_out = similar(coarse_in)
        patch_in = create_conservative_tree_patch_2d(3:5, 4:6)
        patch_out = create_conservative_tree_patch_2d(3:5, 4:6)

        for q in 1:9, j in axes(coarse_in, 2), i in axes(coarse_in, 1)
            if !(i in patch_in.parent_i_range && j in patch_in.parent_j_range)
                coarse_in[i, j, q] = q + i / 32 + j / 64 + i * j / 4096
            end
        end
        for q in 1:9, j in axes(patch_in.fine_F, 2), i in axes(patch_in.fine_F, 1)
            patch_in.fine_F[i, j, q] = q / 2 + i / 128 + j / 256 + i * j / 8192
        end
        _stream_zero_boundary_packets!(coarse_in, patch_in, topology)
        pop0 = active_population_sums_F(coarse_in, patch_in)

        stream_composite_routes_interior_F_2d!(
            coarse_out, patch_out, coarse_in, patch_in, topology)

        @test isapprox(active_population_sums_F(coarse_out, patch_out), pop0;
                       atol=1e-12, rtol=0)
        @test isapprox(active_mass_F(coarse_out, patch_out), active_mass_F(coarse_in, patch_in);
                       atol=1e-12, rtol=0)
    end

    @testset "layout checks catch mismatched patch" begin
        coarse_in = zeros(Float64, Nx, Ny, 9)
        coarse_out = similar(coarse_in)
        wrong_patch = create_conservative_tree_patch_2d(4:6, 4:6)
        @test_throws ArgumentError stream_composite_routes_interior_F_2d!(
            coarse_out, wrong_patch, coarse_in, patch_in, topology)
    end

    @testset "periodic x wraps coarse boundary packets" begin
        coarse_in = zeros(Float64, Nx, Ny, 9)
        coarse_out = similar(coarse_in)
        patch_in = create_conservative_tree_patch_2d(3:5, 4:6)
        patch_out = create_conservative_tree_patch_2d(3:5, 4:6)
        coarse_in[1, 5, 4] = 2.5
        coarse_in[Nx, 5, 2] = 3.5

        stream_composite_routes_periodic_x_F_2d!(
            coarse_out, patch_out, coarse_in, patch_in, topology)

        @test coarse_out[Nx, 5, 4] == 2.5
        @test coarse_out[1, 5, 2] == 3.5
        @test isapprox(active_mass_F(coarse_out, patch_out), 6.0; atol=1e-14, rtol=0)
    end

    @testset "periodic x wraps coarse boundary packets into fine patch" begin
        nx, ny = 6, 6
        patch_in = create_conservative_tree_patch_2d(5:6, 3:4)
        patch_out = create_conservative_tree_patch_2d(5:6, 3:4)
        topology = create_conservative_tree_topology_2d(nx, ny, patch_in)
        coarse_in = zeros(Float64, nx, ny, 9)
        coarse_out = similar(coarse_in)
        coarse_in[1, 3, 4] = 8.0

        stream_composite_routes_periodic_x_F_2d!(
            coarse_out, patch_out, coarse_in, patch_in, topology)

        @test patch_out.fine_F[4, 1, 4] == 2.0
        @test patch_out.fine_F[4, 2, 4] == 2.0
        @test coarse_out[1, 3, 4] == 4.0
        @test isapprox(active_mass_F(coarse_out, patch_out), 8.0; atol=1e-14, rtol=0)
    end

    @testset "periodic x wraps fine boundary packets to coarse cells" begin
        nx, ny = 6, 6
        patch_in = create_conservative_tree_patch_2d(1:2, 3:4)
        patch_out = create_conservative_tree_patch_2d(1:2, 3:4)
        topology = create_conservative_tree_topology_2d(nx, ny, patch_in)
        coarse_in = zeros(Float64, nx, ny, 9)
        coarse_out = similar(coarse_in)
        patch_in.fine_F[1, 1, 4] = 6.0

        stream_composite_routes_periodic_x_F_2d!(
            coarse_out, patch_out, coarse_in, patch_in, topology)

        @test coarse_out[nx, 3, 4] == 6.0
        @test isapprox(active_mass_F(coarse_out, patch_out), 6.0; atol=1e-14, rtol=0)
    end

    @testset "periodic x keeps all non-y-boundary population sums" begin
        coarse_in = zeros(Float64, Nx, Ny, 9)
        coarse_out = similar(coarse_in)
        patch_in = create_conservative_tree_patch_2d(3:5, 4:6)
        patch_out = create_conservative_tree_patch_2d(3:5, 4:6)
        topology = create_conservative_tree_topology_2d(Nx, Ny, patch_in)

        for q in 1:9, j in axes(coarse_in, 2), i in axes(coarse_in, 1)
            if !(i in patch_in.parent_i_range && j in patch_in.parent_j_range)
                coarse_in[i, j, q] = 0.25q + i / 41 + j / 67 + i * j / 8192
            end
        end
        for q in 1:9, j in axes(patch_in.fine_F, 2), i in axes(patch_in.fine_F, 1)
            patch_in.fine_F[i, j, q] = 0.125q + i / 131 + j / 257 + i * j / 16384
        end
        _stream_zero_y_boundary_packets!(coarse_in, patch_in, topology)
        pop0 = active_population_sums_F(coarse_in, patch_in)

        stream_composite_routes_periodic_x_F_2d!(
            coarse_out, patch_out, coarse_in, patch_in, topology)

        @test isapprox(active_population_sums_F(coarse_out, patch_out), pop0;
                       atol=1e-12, rtol=0)
        @test isapprox(active_mass_F(coarse_out, patch_out), active_mass_F(coarse_in, patch_in);
                       atol=1e-12, rtol=0)
    end

    @testset "periodic x wall y bounces coarse boundary packets" begin
        coarse_in = zeros(Float64, Nx, Ny, 9)
        coarse_out = similar(coarse_in)
        patch_in = create_conservative_tree_patch_2d(3:5, 4:6)
        patch_out = create_conservative_tree_patch_2d(3:5, 4:6)
        topology = create_conservative_tree_topology_2d(Nx, Ny, patch_in)
        coarse_in[4, 1, 5] = 2.0
        coarse_in[6, Ny, 3] = 3.0
        coarse_in[1, 1, 8] = 4.0

        stream_composite_routes_periodic_x_wall_y_F_2d!(
            coarse_out, patch_out, coarse_in, patch_in, topology)

        @test coarse_out[4, 1, d2q9_opposite(5)] == 2.0
        @test coarse_out[6, Ny, d2q9_opposite(3)] == 3.0
        @test coarse_out[1, 1, d2q9_opposite(8)] == 4.0
        @test isapprox(active_mass_F(coarse_out, patch_out), 9.0; atol=1e-14, rtol=0)
    end

    @testset "periodic x wall y bounces fine boundary packets" begin
        nx, ny = 6, 6
        patch_in = create_conservative_tree_patch_2d(3:4, 5:6)
        patch_out = create_conservative_tree_patch_2d(3:4, 5:6)
        topology = create_conservative_tree_topology_2d(nx, ny, patch_in)
        coarse_in = zeros(Float64, nx, ny, 9)
        coarse_out = similar(coarse_in)
        patch_in.fine_F[2, 4, 3] = 7.0

        stream_composite_routes_periodic_x_wall_y_F_2d!(
            coarse_out, patch_out, coarse_in, patch_in, topology)

        @test patch_out.fine_F[2, 4, d2q9_opposite(3)] == 7.0
        @test isapprox(active_mass_F(coarse_out, patch_out), 7.0; atol=1e-14, rtol=0)
    end

    @testset "periodic x wall y conserves total active mass" begin
        coarse_in = zeros(Float64, Nx, Ny, 9)
        coarse_out = similar(coarse_in)
        patch_in = create_conservative_tree_patch_2d(3:5, 4:6)
        patch_out = create_conservative_tree_patch_2d(3:5, 4:6)
        topology = create_conservative_tree_topology_2d(Nx, Ny, patch_in)

        for q in 1:9, j in axes(coarse_in, 2), i in axes(coarse_in, 1)
            if !(i in patch_in.parent_i_range && j in patch_in.parent_j_range)
                coarse_in[i, j, q] = 0.375q + i / 43 + j / 71 + i * j / 8192
            end
        end
        for q in 1:9, j in axes(patch_in.fine_F, 2), i in axes(patch_in.fine_F, 1)
            patch_in.fine_F[i, j, q] = 0.1875q + i / 137 + j / 263 + i * j / 16384
        end
        mass0 = active_mass_F(coarse_in, patch_in)

        stream_composite_routes_periodic_x_wall_y_F_2d!(
            coarse_out, patch_out, coarse_in, patch_in, topology)

        @test isapprox(active_mass_F(coarse_out, patch_out), mass0; atol=1e-11, rtol=0)
    end

    @testset "periodic x moving wall y corrects coarse diagonals" begin
        coarse_in = zeros(Float64, Nx, Ny, 9)
        coarse_out = similar(coarse_in)
        patch_in = create_conservative_tree_patch_2d(3:5, 4:6)
        patch_out = create_conservative_tree_patch_2d(3:5, 4:6)
        topology = create_conservative_tree_topology_2d(Nx, Ny, patch_in)
        u_south = 0.06
        u_north = 0.03
        coarse_in[4, 1, 8] = 2.0
        coarse_in[6, Ny, 6] = 3.0

        stream_composite_routes_periodic_x_moving_wall_y_F_2d!(
            coarse_out, patch_out, coarse_in, patch_in, topology;
            u_south=u_south, u_north=u_north)

        @test coarse_out[4, 1, 6] ==
              2.0 + _stream_moving_wall_delta(1.0, 1.0, u_south, 6)
        @test coarse_out[6, Ny, 8] ==
              3.0 + _stream_moving_wall_delta(1.0, 1.0, u_north, 8)
    end

    @testset "periodic x moving wall y corrects fine diagonals" begin
        nx, ny = 6, 6
        patch_in = create_conservative_tree_patch_2d(3:4, 5:6)
        patch_out = create_conservative_tree_patch_2d(3:4, 5:6)
        topology = create_conservative_tree_topology_2d(nx, ny, patch_in)
        coarse_in = zeros(Float64, nx, ny, 9)
        coarse_out = similar(coarse_in)
        u_north = 0.08
        patch_in.fine_F[2, 4, 6] = 5.0

        stream_composite_routes_periodic_x_moving_wall_y_F_2d!(
            coarse_out, patch_out, coarse_in, patch_in, topology;
            u_north=u_north, volume_fine=0.25)

        @test patch_out.fine_F[2, 4, 8] ==
              5.0 + _stream_moving_wall_delta(0.25, 1.0, u_north, 8)
    end

    @testset "periodic x moving wall y injects tangential momentum at rest" begin
        coarse_in = zeros(Float64, Nx, Ny, 9)
        coarse_out = similar(coarse_in)
        patch_in = create_conservative_tree_patch_2d(3:5, 4:6)
        patch_out = create_conservative_tree_patch_2d(3:5, 4:6)
        topology = create_conservative_tree_topology_2d(Nx, Ny, patch_in)
        fill_equilibrium_integrated_D2Q9!(coarse_in, 1.0, 1.0, 0.0, 0.0)
        fill_equilibrium_integrated_D2Q9!(patch_in.fine_F, 0.25, 1.0, 0.0, 0.0)
        mass0 = active_mass_F(coarse_in, patch_in)
        mx0 = active_momentum_F(coarse_in, patch_in)[1]

        stream_composite_routes_periodic_x_moving_wall_y_F_2d!(
            coarse_out, patch_out, coarse_in, patch_in, topology;
            u_north=0.04, volume_coarse=1.0, volume_fine=0.25)

        @test isapprox(active_mass_F(coarse_out, patch_out), mass0; atol=1e-12, rtol=0)
        @test active_momentum_F(coarse_out, patch_out)[1] > mx0
    end

    @testset "route native Poiseuille smoke accelerates and conserves mass" begin
        nx, ny = 18, 14
        patch = create_conservative_tree_patch_2d(7:12, 5:10)
        patch_next = create_conservative_tree_patch_2d(7:12, 5:10)
        topology = create_conservative_tree_topology_2d(nx, ny, patch)
        coarse = zeros(Float64, nx, ny, 9)
        coarse_next = similar(coarse)
        fill_equilibrium_integrated_D2Q9!(coarse, 1.0, 1.0, 0.0, 0.0)
        fill_equilibrium_integrated_D2Q9!(patch.fine_F, 0.25, 1.0, 0.0, 0.0)
        mass0 = active_mass_F(coarse, patch)

        for _ in 1:160
            collide_Guo_composite_F_2d!(coarse, patch, 1.0, 0.25, 1.0, 1.0, 5e-5, 0.0)
            stream_composite_routes_periodic_x_wall_y_F_2d!(
                coarse_next, patch_next, coarse, patch, topology)
            coarse, coarse_next = coarse_next, coarse
            patch, patch_next = patch_next, patch
        end

        @test isapprox(active_mass_F(coarse, patch), mass0; atol=1e-10, rtol=0)
        @test active_momentum_F(coarse, patch)[1] > 0.5
    end

    @testset "route native Couette smoke injects positive momentum" begin
        nx, ny = 18, 14
        patch = create_conservative_tree_patch_2d(7:12, 5:10)
        patch_next = create_conservative_tree_patch_2d(7:12, 5:10)
        topology = create_conservative_tree_topology_2d(nx, ny, patch)
        coarse = zeros(Float64, nx, ny, 9)
        coarse_next = similar(coarse)
        fill_equilibrium_integrated_D2Q9!(coarse, 1.0, 1.0, 0.0, 0.0)
        fill_equilibrium_integrated_D2Q9!(patch.fine_F, 0.25, 1.0, 0.0, 0.0)
        mass0 = active_mass_F(coarse, patch)

        for _ in 1:160
            collide_BGK_composite_F_2d!(coarse, patch, 1.0, 0.25, 1.0, 1.0)
            stream_composite_routes_periodic_x_moving_wall_y_F_2d!(
                coarse_next, patch_next, coarse, patch, topology;
                u_north=0.04, volume_coarse=1.0, volume_fine=0.25)
            coarse, coarse_next = coarse_next, coarse
            patch, patch_next = patch_next, patch
        end

        @test isapprox(active_mass_F(coarse, patch), mass0; atol=1e-10, rtol=0)
        @test active_momentum_F(coarse, patch)[1] > 1.0
    end

    @testset "integrated Zou-He cell closures impose moments" begin
        volume = 0.25
        west = zeros(Float64, 9)
        fill_equilibrium_integrated_D2Q9!(west, volume, 1.0, 0.0, 0.0)
        apply_zou_he_west_cell_F_2d!(west, 0.04, volume)
        rho_west = mass_F(west) / volume
        ux_west = (momentum_F(west)[1] / volume) / rho_west

        east = zeros(Float64, 9)
        fill_equilibrium_integrated_D2Q9!(east, volume, 1.0, 0.02, 0.0)
        apply_zou_he_pressure_east_cell_F_2d!(east, volume; rho_out=1.1)
        rho_east = mass_F(east) / volume

        @test isapprox(ux_west, 0.04; atol=1e-14, rtol=0)
        @test isapprox(rho_east, 1.1; atol=1e-14, rtol=0)
        @test_throws ArgumentError apply_zou_he_west_cell_F_2d!(zeros(8), 0.04, volume)
        @test_throws ArgumentError apply_zou_he_pressure_east_cell_F_2d!(
            zeros(8), volume; rho_out=1.0)
    end

    @testset "composite Zou-He closures target active boundary cells" begin
        nx, ny = 5, 4
        volume_coarse = 1.0
        volume_fine = 0.25

        coarse = zeros(Float64, nx, ny, 9)
        patch = create_conservative_tree_patch_2d(3:4, 2:3)
        fill_equilibrium_integrated_D2Q9!(coarse, volume_coarse, 1.0, 0.0, 0.0)
        fill_equilibrium_integrated_D2Q9!(patch.fine_F, volume_fine, 1.0, 0.0, 0.0)
        apply_composite_zou_he_west_F_2d!(
            coarse, patch, 0.03, volume_coarse, volume_fine)
        for j in 1:ny
            cell = @view coarse[1, j, :]
            rho = mass_F(cell) / volume_coarse
            ux = (momentum_F(cell)[1] / volume_coarse) / rho
            @test isapprox(ux, 0.03; atol=1e-14, rtol=0)
        end

        patch_west = create_conservative_tree_patch_2d(1:2, 1:ny)
        coarse_west = zeros(Float64, nx, ny, 9)
        fill_equilibrium_integrated_D2Q9!(coarse_west, volume_coarse, 1.0, 0.0, 0.0)
        fill_equilibrium_integrated_D2Q9!(patch_west.fine_F, volume_fine, 1.0, 0.0, 0.0)
        apply_composite_zou_he_west_F_2d!(
            coarse_west, patch_west, 0.03, volume_coarse, volume_fine)
        for jf in axes(patch_west.fine_F, 2)
            cell = @view patch_west.fine_F[1, jf, :]
            rho = mass_F(cell) / volume_fine
            ux = (momentum_F(cell)[1] / volume_fine) / rho
            @test isapprox(ux, 0.03; atol=1e-14, rtol=0)
        end

        patch_east = create_conservative_tree_patch_2d(4:5, 1:ny)
        coarse_east = zeros(Float64, nx, ny, 9)
        fill_equilibrium_integrated_D2Q9!(coarse_east, volume_coarse, 1.0, 0.02, 0.0)
        fill_equilibrium_integrated_D2Q9!(patch_east.fine_F, volume_fine, 1.0, 0.02, 0.0)
        apply_composite_zou_he_pressure_east_F_2d!(
            coarse_east, patch_east, volume_coarse, volume_fine; rho_out=1.08)
        for jf in axes(patch_east.fine_F, 2)
            cell = @view patch_east.fine_F[end, jf, :]
            @test isapprox(mass_F(cell) / volume_fine, 1.08; atol=1e-14, rtol=0)
        end
    end

    @testset "open x route smoke applies composite Zou-He closures" begin
        nx, ny = 8, 6
        volume_coarse = 1.0
        volume_fine = 0.25
        patch = create_conservative_tree_patch_2d(3:5, 2:5)
        patch_next = create_conservative_tree_patch_2d(3:5, 2:5)
        topology = create_conservative_tree_topology_2d(nx, ny, patch)
        coarse = zeros(Float64, nx, ny, 9)
        coarse_next = similar(coarse)
        fill_equilibrium_integrated_D2Q9!(coarse, volume_coarse, 1.0, 0.0, 0.0)
        fill_equilibrium_integrated_D2Q9!(patch.fine_F, volume_fine, 1.0, 0.0, 0.0)

        stream_composite_routes_zou_he_x_wall_y_F_2d!(
            coarse_next, patch_next, coarse, patch, topology;
            u_in=0.03, rho_out=1.0,
            volume_coarse=volume_coarse, volume_fine=volume_fine)

        for j in 1:ny
            west = @view coarse_next[1, j, :]
            east = @view coarse_next[nx, j, :]
            rho_west = mass_F(west) / volume_coarse
            ux_west = (momentum_F(west)[1] / volume_coarse) / rho_west
            @test isapprox(ux_west, 0.03; atol=1e-14, rtol=0)
            @test isapprox(mass_F(east) / volume_coarse, 1.0; atol=1e-14, rtol=0)
        end
        @test active_mass_F(coarse_next, patch_next) > 0
    end

    @testset "route native Couette runner has Phase-P profile gates" begin
        result = run_conservative_tree_couette_route_native_2d()

        @test result.flow == :couette_route_native
        @test result.steps == 3000
        @test abs(result.mass_drift) < 1e-9
        @test result.l2_error < 7e-3
        @test result.linf_error < 8e-3
        @test result.ux_profile[end] > result.ux_profile[1] + 0.035
        @test all(isfinite, result.ux_profile)
    end

    @testset "route native Poiseuille runner has Phase-P shape gates" begin
        result = run_conservative_tree_poiseuille_route_native_2d()
        center = div(length(result.ux_profile), 2)
        left = result.ux_profile[2:center]
        right = reverse(result.ux_profile[center+1:end-1])
        ncmp = min(length(left), length(right))

        @test result.flow == :poiseuille_route_native
        @test result.steps == 3000
        @test abs(result.mass_drift) < 1e-9
        @test result.l2_error < 2e-2
        @test result.linf_error < 2.5e-2
        @test result.ux_profile[center] > 0.006
        @test result.ux_profile[center] > result.ux_profile[2] + 0.005
        @test result.ux_profile[center] > result.ux_profile[end-1] + 0.005
        @test maximum(abs.(left[1:ncmp] .- right[1:ncmp])) < 3e-3
    end

    @testset "route native open channel smoke remains bounded" begin
        result = run_conservative_tree_open_channel_route_native_2d()

        @test result.flow == :open_channel_route_native
        @test result.steps == 160
        @test isfinite(result.mass_drift)
        @test abs(result.mass_drift) / result.mass_initial < 0.2
        @test result.ux_mean > 0.005
    end

    @testset "route native Phase P validation compares oracle runners" begin
        report = validate_conservative_tree_route_native_phase_p_2d(; steps=1000)

        @test report.couette.route.flow == :couette_route_native
        @test report.couette.oracle.flow == :couette
        @test abs(report.couette.route.mass_drift) < 1e-9
        @test report.couette.l2_delta < 7e-3
        @test report.couette.linf_delta < 9e-3
        @test report.poiseuille.route.flow == :poiseuille_route_native
        @test report.poiseuille.oracle.flow == :poiseuille
        @test abs(report.poiseuille.route.mass_drift) < 1e-9
        @test report.poiseuille.l2_delta < 1.5e-2
        @test report.poiseuille.linf_delta < 2e-2
    end

    @testset "solid route bounces fine packets at obstacle links" begin
        nx, ny = 8, 8
        patch_in = create_conservative_tree_patch_2d(3:5, 4:6)
        patch_out = create_conservative_tree_patch_2d(3:5, 4:6)
        topology = create_conservative_tree_topology_2d(nx, ny, patch_in)
        coarse_in = zeros(Float64, nx, ny, 9)
        coarse_out = similar(coarse_in)
        is_solid = falses(2 * nx, 2 * ny)
        is_solid[7, 8] = true
        patch_in.fine_F[2, 2, 2] = 5.0

        stream_composite_routes_periodic_x_wall_y_solid_F_2d!(
            coarse_out, patch_out, coarse_in, patch_in, topology, is_solid)

        @test patch_out.fine_F[2, 2, d2q9_opposite(2)] == 5.0
        @test patch_out.fine_F[3, 2, 2] == 0.0
        @test isapprox(_stream_composite_fluid_mass(coarse_out, patch_out, is_solid),
                       _stream_composite_fluid_mass(coarse_in, patch_in, is_solid);
                       atol=1e-14, rtol=0)
    end

    @testset "solid layout rejects partially solid active coarse cells" begin
        nx, ny = 8, 8
        patch_in = create_conservative_tree_patch_2d(3:5, 4:6)
        patch_out = create_conservative_tree_patch_2d(3:5, 4:6)
        topology = create_conservative_tree_topology_2d(nx, ny, patch_in)
        coarse_in = zeros(Float64, nx, ny, 9)
        coarse_out = similar(coarse_in)
        is_solid = falses(2 * nx, 2 * ny)
        is_solid[1, 1] = true

        @test_throws ArgumentError stream_composite_routes_periodic_x_wall_y_solid_F_2d!(
            coarse_out, patch_out, coarse_in, patch_in, topology, is_solid)
    end

    @testset "route native square obstacle macroflow is conservative" begin
        result = run_conservative_tree_square_obstacle_route_native_2d(; steps=700)

        @test result.flow == :square_obstacle_route_native
        @test result.steps == 700
        @test abs(result.mass_drift) < 1e-8
        @test result.ux_mean > 0
        @test abs(result.uy_mean) < 1e-10
        @test sum(result.is_solid_leaf) > 0
    end

    @testset "route native cylinder obstacle benchmark canary is finite" begin
        result = run_conservative_tree_cylinder_obstacle_route_native_2d(
            ; steps=120, avg_window=40)

        @test result.steps == 120
        @test result.avg_window == 40
        @test isfinite(result.Cd)
        @test abs(result.mass_drift) < 1e-8
        @test result.u_ref > 0
        @test sum(result.is_solid_leaf) > 0
    end

    @testset "cartesian versus AMR benchmark rows are finite" begin
        rows = benchmark_conservative_tree_cartesian_vs_amr_2d(
            ; flows=(:bfs, :square, :cylinder), steps=20)

        @test length(rows) == 6
        @test Set(row.flow for row in rows) == Set([:bfs, :square, :cylinder])
        @test Set(row.method for row in rows) == Set([:leaf_oracle, :amr_route_native])
        @test all(row.steps == 20 for row in rows)
        @test all(isfinite(row.ux_mean) for row in rows)
        @test all(isfinite(row.uy_mean) for row in rows)
        @test all(isfinite(row.mass_rel_drift) for row in rows)
        @test all(row.elapsed_s >= 0 for row in rows)
    end

    @testset "vertical facing step mask is wall attached" begin
        mask = vertical_facing_step_solid_mask_leaf_2d(16, 12, 7:9, 5)

        @test sum(mask) == 15
        @test mask[7, 1]
        @test mask[9, 5]
        @test !mask[7, 6]
        @test !mask[6, 1]
        @test_throws ArgumentError vertical_facing_step_solid_mask_leaf_2d(16, 12, 0:2, 5)
        @test_throws ArgumentError vertical_facing_step_solid_mask_leaf_2d(16, 12, 7:9, 12)
    end

    @testset "route native VFS macroflow is conservative" begin
        result = run_conservative_tree_vfs_route_native_2d(; steps=500)

        @test result.flow == :vfs_route_native
        @test result.steps == 500
        @test abs(result.mass_drift) < 1e-8
        @test result.ux_mean > 0
        @test abs(result.uy_mean) < 5e-3
        @test sum(result.is_solid_leaf) > 0
    end

    @testset "mask adaptive route native VFS regrids around step" begin
        result = run_conservative_tree_vfs_mask_adaptive_route_native_2d()

        @test result.flow == :vfs_mask_adaptive_route_native
        @test result.steps == 500
        @test result.regrid_every == 120
        @test result.regrid_count == 1
        @test result.patch_history[1] == (5:12, 1:8)
        @test result.patch_history[2] == (6:11, 1:5)
        @test abs(result.mass_drift) < 1e-8
        @test result.ux_mean > 0
        @test abs(result.uy_mean) < 5e-3
        @test sum(result.is_solid_leaf) > 0
    end

    @testset "solid mask patch indicator is pure and padded" begin
        is_solid = falses(20, 18)
        is_solid[13:16, 9:12] .= true

        ranges = conservative_tree_solid_mask_patch_range_2d(is_solid; pad=1)
        tight = conservative_tree_solid_mask_patch_range_2d(is_solid; pad=0)

        @test ranges.i_range == 6:9
        @test ranges.j_range == 4:7
        @test tight.i_range == 7:8
        @test tight.j_range == 5:6
        @test count(is_solid) == 16

        border = falses(20, 18)
        border[1:2, 17:18] .= true
        clamped = conservative_tree_solid_mask_patch_range_2d(border; pad=2)
        @test clamped.i_range == 1:3
        @test clamped.j_range == 7:9

        @test_throws ArgumentError conservative_tree_solid_mask_patch_range_2d(
            falses(19, 18))
        @test_throws ArgumentError conservative_tree_solid_mask_patch_range_2d(
            falses(20, 18))
        @test_throws ArgumentError conservative_tree_solid_mask_patch_range_2d(
            is_solid; pad=-1)
    end

    @testset "patch range hysteresis prevents one cell oscillation" begin
        current = (i_range=4:10, j_range=3:8)
        near_shrink = conservative_tree_hysteresis_patch_range_2d(
            current.i_range, current.j_range, 5:9, 4:7; shrink_margin=2)
        deep_shrink = conservative_tree_hysteresis_patch_range_2d(
            current.i_range, current.j_range, 6:8, 5:6; shrink_margin=2)
        grow = conservative_tree_hysteresis_patch_range_2d(
            current.i_range, current.j_range, 3:10, 3:8; shrink_margin=2)
        no_hysteresis = conservative_tree_hysteresis_patch_range_2d(
            current.i_range, current.j_range, 5:9, 4:7; shrink_margin=0)

        @test near_shrink == current
        @test deep_shrink == (i_range=6:8, j_range=5:6)
        @test grow == (i_range=3:10, j_range=3:8)
        @test no_hysteresis == (i_range=5:9, j_range=4:7)
        @test_throws ArgumentError conservative_tree_hysteresis_patch_range_2d(
            4:10, 3:8, 5:4, 4:7)
        @test_throws ArgumentError conservative_tree_hysteresis_patch_range_2d(
            4:10, 3:8, 5:9, 4:7; shrink_margin=-1)
    end

    @testset "scalar indicator selects padded patch range" begin
        indicator = zeros(Float64, 10, 9)
        indicator[6:8, 4:5] .= 0.75
        indicator[4, 6] = -0.9

        ranges = conservative_tree_indicator_patch_range_2d(
            indicator; threshold=0.5, pad=1)
        tight = conservative_tree_indicator_patch_range_2d(
            indicator; threshold=0.5, pad=0)

        @test ranges.i_range == 3:9
        @test ranges.j_range == 3:7
        @test tight.i_range == 4:8
        @test tight.j_range == 4:6

        border = zeros(Float64, 10, 9)
        border[1, 9] = 1.0
        clamped = conservative_tree_indicator_patch_range_2d(
            border; threshold=0.5, pad=3)
        @test clamped.i_range == 1:4
        @test clamped.j_range == 6:9

        bad = copy(indicator)
        bad[2, 2] = NaN
        @test_throws ArgumentError conservative_tree_indicator_patch_range_2d(
            indicator; threshold=1.0)
        @test_throws ArgumentError conservative_tree_indicator_patch_range_2d(
            indicator; threshold=-0.1)
        @test_throws ArgumentError conservative_tree_indicator_patch_range_2d(
            indicator; threshold=Inf)
        @test_throws ArgumentError conservative_tree_indicator_patch_range_2d(
            indicator; threshold=0.5, pad=-1)
        @test_throws ArgumentError conservative_tree_indicator_patch_range_2d(
            bad; threshold=0.5)
    end

    @testset "gradient indicator matches linear fields" begin
        field = [2.0 * i - 3.0 * j for i in 1:7, j in 1:6]
        indicator = conservative_tree_gradient_indicator_2d(field)

        @test size(indicator) == size(field)
        @test maximum(abs.(indicator .- sqrt(13.0))) < 1e-14
        @test all(iszero, conservative_tree_gradient_indicator_2d(ones(4, 5)))

        line = reshape(collect(0.0:3.0:12.0), 1, 5)
        @test conservative_tree_gradient_indicator_2d(line) == fill(3.0, 1, 5)

        bad = copy(field)
        bad[3, 3] = Inf
        @test_throws ArgumentError conservative_tree_gradient_indicator_2d(
            zeros(Float64, 0, 2))
        @test_throws ArgumentError conservative_tree_gradient_indicator_2d(bad)
    end

    @testset "composite velocity field feeds gradient patch selector" begin
        nx, ny = 6, 5
        volume_leaf = 0.25
        rho = 1.0
        coarse = zeros(Float64, nx, ny, 9)
        patch = create_conservative_tree_patch_2d(1:nx, 1:ny)
        ux_ref = zeros(Float64, 2 * nx, 2 * ny)
        ux_ref[7:8, 4:6] .= 0.04

        for j in axes(patch.fine_F, 2), i in axes(patch.fine_F, 1)
            fill_equilibrium_integrated_D2Q9!(
                @view(patch.fine_F[i, j, :]), volume_leaf, rho, ux_ref[i, j], 0.0)
        end

        velocity = composite_leaf_velocity_field_2d(
            coarse, patch; volume_leaf=volume_leaf)
        indicator = conservative_tree_gradient_indicator_2d(velocity.ux)
        ranges = conservative_tree_indicator_patch_range_2d(
            indicator; threshold=0.015, pad=0)

        @test maximum(abs.(velocity.ux .- ux_ref)) < 1e-14
        @test maximum(abs.(velocity.uy)) < 1e-14
        @test ranges.i_range == 6:9
        @test ranges.j_range == 3:7
    end

    @testset "velocity gradient adaptation regrids conservatively" begin
        nx, ny = 6, 5
        volume_leaf = 0.25
        coarse = zeros(Float64, nx, ny, 9)
        patch = create_conservative_tree_patch_2d(1:nx, 1:ny)
        ux_ref = zeros(Float64, 2 * nx, 2 * ny)
        ux_ref[7:8, 4:6] .= 0.04
        for j in axes(patch.fine_F, 2), i in axes(patch.fine_F, 1)
            fill_equilibrium_integrated_D2Q9!(
                @view(patch.fine_F[i, j, :]), volume_leaf, 1.0, ux_ref[i, j], 0.0)
        end
        pop0 = active_population_sums_F(coarse, patch)

        ranges = conservative_tree_velocity_gradient_patch_range_2d(
            coarse, patch; threshold=0.015, volume_leaf=volume_leaf)
        adapted = adapt_conservative_tree_patch_to_velocity_gradient_2d(
            coarse, patch; threshold=0.015, volume_leaf=volume_leaf)

        @test ranges == (i_range=3:5, j_range=2:4)
        @test adapted.patch.parent_i_range == 3:5
        @test adapted.patch.parent_j_range == 2:4
        @test isapprox(active_population_sums_F(adapted.coarse_F, adapted.patch),
                       pop0; atol=1e-11, rtol=0)
        @test_throws ArgumentError conservative_tree_velocity_gradient_patch_range_2d(
            coarse, patch; threshold=1.0, volume_leaf=volume_leaf)
    end

    @testset "mask-driven patch adaptation conserves active populations" begin
        nx, ny = 10, 9
        patch = create_conservative_tree_patch_2d(2:4, 2:4)
        coarse = zeros(Float64, nx, ny, 9)
        for q in 1:9, j in axes(coarse, 2), i in axes(coarse, 1)
            if !(i in patch.parent_i_range && j in patch.parent_j_range)
                coarse[i, j, q] = 0.2 + q / 31 + i / 47 + j / 59 + i * j / 4096
            end
        end
        for q in 1:9, j in axes(patch.fine_F, 2), i in axes(patch.fine_F, 1)
            patch.fine_F[i, j, q] = 0.05 + q / 67 + i / 101 + j / 131 + i * j / 8192
        end
        pop0 = active_population_sums_F(coarse, patch)
        is_solid = falses(2 * nx, 2 * ny)
        is_solid[13:16, 9:12] .= true

        adapted = adapt_conservative_tree_patch_to_solid_mask_2d(
            coarse, patch, is_solid; pad=1)

        @test adapted.patch.parent_i_range == 6:9
        @test adapted.patch.parent_j_range == 4:7
        @test isapprox(active_population_sums_F(adapted.coarse_F, adapted.patch), pop0;
                       atol=1e-11, rtol=0)
        @test_throws ArgumentError adapt_conservative_tree_patch_to_solid_mask_2d(
            coarse, patch, falses(2 * nx, 2 * ny))
    end

    @testset "direct regrid matches leaf oracle for grow shrink and shift" begin
        nx, ny = 11, 10
        patch = create_conservative_tree_patch_2d(3:5, 4:6)
        coarse = zeros(Float64, nx, ny, 9)
        for q in 1:9, j in axes(coarse, 2), i in axes(coarse, 1)
            if !(i in patch.parent_i_range && j in patch.parent_j_range)
                coarse[i, j, q] = 0.12 + q / 29 + i / 53 + j / 61 + i * j / 5000
            end
        end
        for q in 1:9, j in axes(patch.fine_F, 2), i in axes(patch.fine_F, 1)
            patch.fine_F[i, j, q] = 0.03 + q / 71 + i / 107 + j / 139 + i * j / 9000
        end

        for ranges in ((2:7, 3:8), (4:4, 5:5), (6:9, 2:4))
            patch_oracle = create_conservative_tree_patch_2d(ranges[1], ranges[2])
            coarse_oracle = similar(coarse)
            patch_direct = create_conservative_tree_patch_2d(ranges[1], ranges[2])
            coarse_direct = similar(coarse)

            regrid_conservative_tree_patch_F_2d!(
                coarse_oracle, patch_oracle, coarse, patch)
            regrid_conservative_tree_patch_direct_F_2d!(
                coarse_direct, patch_direct, coarse, patch)

            @test isapprox(coarse_direct, coarse_oracle; atol=1e-13, rtol=0)
            @test isapprox(patch_direct.fine_F, patch_oracle.fine_F; atol=1e-13, rtol=0)
            @test isapprox(active_population_sums_F(coarse_direct, patch_direct),
                           active_population_sums_F(coarse, patch); atol=1e-11, rtol=0)
        end
    end

    @testset "adaptive route native Poiseuille regrids conservatively" begin
        result = run_conservative_tree_poiseuille_adaptive_route_native_2d()

        @test result.flow == :poiseuille_adaptive_route_native
        @test result.steps == 320
        @test result.regrid_every == 80
        @test result.regrid_count == 3
        @test length(result.patch_history) == 4
        @test result.patch_history[1] == (7:12, 5:10)
        @test result.patch_history[2] == (6:11, 4:9)
        @test result.patch_history[3] == (8:13, 5:10)
        @test result.patch_history[4] == (7:12, 5:10)
        @test abs(result.mass_drift) < 1e-9
        @test result.ux_mean > 0
    end

    @testset "gradient adaptive route native Poiseuille regrids conservatively" begin
        result = run_conservative_tree_poiseuille_gradient_adaptive_route_native_2d()

        @test result.flow == :poiseuille_gradient_adaptive_route_native
        @test result.steps == 320
        @test result.regrid_every == 80
        @test result.regrid_count == 2
        @test result.patch_history[1] == (7:12, 5:10)
        @test result.patch_history[2] == (3:16, 2:13)
        @test result.patch_history[3] == (1:18, 1:14)
        @test abs(result.mass_drift) < 1e-9
        @test result.ux_mean > 0
    end
end
